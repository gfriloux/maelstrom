/*
 * Copyright 2010, 2011, 2012 Mike Blumenkrantz <michael.blumenkrantz@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published
 * by the Free Software Foundation, either version 2.1 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

%name azy_parser_
%token_prefix AZY_
%token_type {Azy_Token *}
%token_destructor {azy_parser_token_free($$);}
%extra_argument {Azy_Parser *parser}
%syntax_error {SYNTAX_ERROR(TOKEN);}
%stack_overflow {
   eina_stringshare_replace(&parser->error, "Parser stack overflow!\n");
}
%include {
#ifdef HAVE_CONFIG_H
# include "config.h"
#endif
#include <stdio.h>
#include "azy_parser_lib.h"
#include "azy_parser.h"
#include "azy.h"
#include <ctype.h>

#define YYCTYPE  char
#define YYCURSOR c
#define YYMARKER q
#define YYCTXMARKER t

#define AZY_EOF     0
#define AZY_UNKNOWN (-1)

#define RET(tok) \
  return azy_parser_token_new(s, tok, c - b)

#define NEXT \
  azy_parser_stream_next(s, c - b); \
  b = c

#define MODEL parser->data
extern void *azy_parser_Alloc(void *(*)(size_t));
extern void  azy_parser_Free(void *,
                             void (*)(void *));
extern void  azy_parser_(void *,
                         int,
                         Azy_Token *,
                         Azy_Parser *);
/* for reference
extern void        azy_parser_Trace(FILE *,
                                    char *);
extern const char *azy_parser_TokenName(int);
*/
static Azy_Token *
azy_parser_stream_to_token(Azy_Stream *s)
{
   const char *c, *b, *q, *t;

   if (s->length == s->index)
     return azy_parser_token_new(s, AZY_EOF, 0);

   c = b = s->buffer + s->index;

main:
/*!re2c

   re2c:indent:top       = 0;
   re2c:indent:string    = "";
   re2c:yyfill:enable    = 0;

   NONPRINT   = [\001-\377];
   ENDINPUT   = "\000";
   D     = [0-9];
   L     = [A-Za-z_];
   C     = [A-Za-z0-9_ !?,.];
   S     = [\041-\172\|~];
   CODESTART   = "<%";
   CODEEND   = "%>";
   SLCOM = "//" (.\ENDINPUT)* ("\n"|ENDINPUT);

   SLCOM              { NEXT; goto main; }
   "/*"("*"*|[^ \t\n\r]*)
{
   while (c++)
     {
        if ((*c == '*') && (c + 1) && (c[1] == '/'))
          {
             c += 2;
             NEXT;
             goto main;
          }
     }
   RET(AZY_UNKNOWN);
}
   CODESTART          { goto code; }
   [ \t\n\r]+         { NEXT; goto main; }
   "__init__"         { RET(AZY_INIT); }
   "__shutdown__"     { RET(AZY_SHUTDOWN); }
   "__attrs__"        { RET(AZY_ATTRS); }
   "__pre__"          { RET(AZY_PRECALL); }
   "__post__"         { RET(AZY_POSTCALL); }
   "__fallback__"     { RET(AZY_FALLBACK); }
   "__download__"     { RET(AZY_DOWNLOAD); }
   "__upload__"       { RET(AZY_UPLOAD); }
   "__version__"      { RET(AZY_VERSION); }
   "Azy_Error"        { RET(AZY_ERROR); }
   "Azy_Namespace"    { RET(AZY_NAMESPACE); }
   "Azy_Module"       { RET(AZY_MODULE); }
   "array"            { RET(AZY_ARRAY); }
   "struct"           { RET(AZY_STRUCT); }
   "{" C+ "}"         { RET(AZY_ERRMSG); }
   [{][ ]S+/[ ][}]    { RET(AZY_STRUCTMEMSTR); }
   L (L|D)*           { RET(AZY_ID); }
   D+                 { RET(AZY_INTEGER); }
   D+"."D+            { RET(AZY_DOUBLE); }
   "="                { RET(AZY_EQ); }
   ";"                { RET(AZY_SEMICOL); }
   "<"                { RET(AZY_LT); }
   ">"                { RET(AZY_GT); }
   "{"                { RET(AZY_LB); }
   "}"                { RET(AZY_RB); }
   "("                { RET(AZY_LP); }
   ")"                { RET(AZY_RP); }
   ","                { RET(AZY_COMMA); }
   ENDINPUT           { RET(AZY_EOF); }
   NONPRINT           { RET(AZY_UNKNOWN); }

 */

code:
/*!re2c
   "%>"                { RET(AZY_CODE); }
   ENDINPUT            { RET(AZY_UNKNOWN); }
   NONPRINT            { goto code; }
 */

     return NULL;
   }

   Azy_Model *azy_parse_string_azy(const char *str, Eina_Bool *err)
   {
      return azy_parser_string_parse_azy(str, err, azy_parser_, azy_parser_Alloc, azy_parser_Free, azy_parser_stream_to_token);
   }
   Azy_Model *azy_parse_file_azy(const char *path, Eina_Bool *err)
   {
      return azy_parser_file_parse_azy(path, err, azy_parser_, azy_parser_Alloc, azy_parser_Free, azy_parser_stream_to_token);
   }

}

compilation_unit ::= opt_namespace_decl toplevel_decls.

%type opt_inline_code {Azy_Token *}
opt_inline_code(Y) ::= . {Y = NULL;}
opt_inline_code(Y) ::= CODE(X). {Y = X;}

%type opt_doc_comment {const char *}
opt_doc_comment(Y) ::= . {Y = NULL;}
opt_doc_comment(Y) ::= DOC(X). {
  Y = X->text;
  X->text = NULL;
  azy_parser_token_free(X);
}

opt_namespace_decl ::= .
opt_namespace_decl ::= namespace_decl.
namespace_decl ::= NAMESPACE ID(N) SEMICOL. {
  if (!MODEL) MODEL = azy_new();
  if (N->text && N->text[0]) MODEL->name = eina_stringshare_add(N->text);
  azy_parser_token_free(N);
}

toplevel_decls ::= toplevel_decl.
toplevel_decls ::= toplevel_decls toplevel_decl.

toplevel_decl ::= error_decl.
toplevel_decl ::= struct_decl(X). {MODEL->types = eina_list_append(MODEL->types, X);}
toplevel_decl ::= module_decl.

/* error with message */
error_decl ::= ERROR ID(N) EQ INTEGER(C) ERRMSG(S) SEMICOL. {
  if (!MODEL) MODEL = azy_new();
  azy_error_new(MODEL, N->text, strtol(C->text, NULL, 10), S->text);
  azy_parser_token_free(N);
  azy_parser_token_free(C);
  azy_parser_token_free(S);
}
/* error without message */
error_decl ::= ERROR ID(N) EQ INTEGER(C) SEMICOL. {
  if (!MODEL) MODEL = azy_new();
  azy_error_new(MODEL, N->text, strtol(C->text, NULL, 10), NULL);
  azy_parser_token_free(N);
  azy_parser_token_free(C);
}

%type struct_decl {Azy_Typedef *}
struct_decl(Y) ::= opt_doc_comment(C) STRUCT ID(N) LB struct_members(M) RB SEMICOL. {
  Azy_Typedef *t;
  if (!MODEL) MODEL = azy_new();
  t = azy_typedef_find(MODEL, N->text);
  if (t && t->struct_members)
    {
      printf("Redefining already defined type %s\n", N->text);
      exit(1);
    }
  else if (!t)
    Y = azy_typedef_new_struct(MODEL, N->text);
  Y->struct_members = M;
  Y->doc = C;
  azy_parser_token_free(N);
}

%type struct_members {Eina_List *}
struct_members(Y) ::= struct_member(X). {Y = eina_list_append(NULL, X);}
struct_members(Y) ::= struct_members(L) struct_member(X). {Y = eina_list_append(L, X);}

/* struct member without special name */
%type struct_member {Azy_Struct_Member *}
struct_member(Y) ::= type(T) ID(N) SEMICOL. {
   Y = calloc(1, sizeof(Azy_Struct_Member));
   Y->type = T;
   Y->name = eina_stringshare_add(N->text);
   azy_parser_token_free(N);
}
/* struct member with special name */
struct_member(Y) ::= type(T) ID(N) STRUCTMEMSTR(S) RB SEMICOL. {
   Y = calloc(1, sizeof(Azy_Struct_Member));
   Y->type = T;
   Y->name = eina_stringshare_add(N->text);
   {
      const char *nospaces = S->text + 1; /* skip LB */

      while (isspace(*nospaces)) nospaces++; /* skip spaces */
      Y->strname = eina_stringshare_add(nospaces);
   }
   azy_parser_token_free(N);
   azy_parser_token_free(S);
}

module_decl ::= module_decl_head module_decl_body.
module_decl_head ::= opt_doc_comment(C) MODULE ID(N). {
   if (!MODEL)MODEL = azy_new();
   MODEL->cur_module = calloc(1, sizeof(Azy_Server_Module));
   MODEL->modules = eina_list_append(MODEL->modules, MODEL->cur_module);
   MODEL->cur_module->name = eina_stringshare_add(N->text);
   MODEL->cur_module->doc = C;
   azy_parser_token_free(N);
}
module_decl_body ::= LB opt_inline_code(C) module_body_decls RB. {
  if (C)
    {
      MODEL->cur_module->stub_header = eina_stringshare_add_length(C->text + 2, strlen(C->text) - 4);
      MODEL->cur_module->stub_header_line = C->sline;
      azy_parser_token_free(C);
    }
  MODEL->cur_module = NULL;
}

module_body_decls ::= module_body_decl.
module_body_decls ::= module_body_decls module_body_decl.

module_body_decl ::= VERSION LP DOUBLE(D) RP SEMICOL.
{
   MODEL->cur_module->version = strtod(D->text, NULL);
   azy_parser_token_free(D);
}
module_body_decl ::= struct_decl(S). {MODEL->types = eina_list_append(MODEL->types, S);}
module_body_decl ::= method_decl(M) CODE(C). {
   MODEL->cur_module->methods = eina_list_append(MODEL->cur_module->methods, M);
   M->stub_impl = eina_stringshare_add_length(C->text + 2, strlen(C->text) - 4);
   M->stub_impl_line = C->sline;
   azy_parser_token_free(C);
}
module_body_decl ::= method_decl(M) SEMICOL. {
  MODEL->cur_module->methods = eina_list_append(MODEL->cur_module->methods, M);
}
module_body_decl ::= INIT CODE(C). {
  MODEL->cur_module->stub_init = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_init_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= SHUTDOWN CODE(C). {
  MODEL->cur_module->stub_shutdown = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_shutdown_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= ATTRS CODE(C). {
  MODEL->cur_module->stub_attrs = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_attrs_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= PRECALL CODE(C). {
  MODEL->cur_module->stub_pre = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_pre_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= POSTCALL CODE(C). {
  MODEL->cur_module->stub_post = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_post_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= FALLBACK CODE(C). {
  MODEL->cur_module->stub_fallback = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_fallback_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= DOWNLOAD CODE(C). {
  MODEL->cur_module->stub_download = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_download_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= UPLOAD CODE(C). {
  MODEL->cur_module->stub_upload = strndup(C->text + 2, strlen(C->text) - 4);
  MODEL->cur_module->stub_upload_line = C->sline;
  azy_parser_token_free(C);
}
module_body_decl ::= error_decl.

%type type {Azy_Typedef *}
type(Y) ::= ARRAY LT type(T) GT. {
  if (!MODEL)
    MODEL = azy_new();
  Y = azy_typedef_new_array(MODEL, T);
}
type(Y) ::= ID(N). {
  if (!MODEL)
    MODEL = azy_new();
  Y = azy_typedef_find(MODEL, N->text);
  if (!Y)
    Y = azy_typedef_new_struct(MODEL, N->text);
  azy_parser_token_free(N);
}

%type method_decl {Azy_Method *}
method_decl(Y) ::= opt_doc_comment(C) type(RT) ID(N) LP params(P) RP. {
  Y = calloc(1, sizeof(Azy_Method));
  Y->name = eina_stringshare_add(N->text);
  Y->return_type = RT;
  Y->params = P;
  Y->doc = C;
  azy_parser_token_free(N);
}

%type params {Eina_List *}
params(Y) ::= . {
  Y = NULL;
}
params(Y) ::= param(X). {
  Y = eina_list_append(NULL, X);
}
params(Y) ::= params(L) COMMA param(X). {
  Y = eina_list_append(L, X);
}

%type opt_owner {int}
opt_owner(Y) ::= . {
  Y = 0;
}

%type param {Azy_Method_Param *}
param(Y) ::= opt_owner(C) type(T) ID(N). {
  Y = calloc(1, sizeof(Azy_Method_Param));
  Y->type = T;
  Y->pass_ownership = C;
  Y->name = eina_stringshare_add(N->text);
  azy_parser_token_free(N);
}
